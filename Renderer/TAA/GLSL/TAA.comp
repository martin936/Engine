#version 450
#extension GL_EXT_samplerless_texture_functions : require


layout(binding = 0) uniform texture2D	MotionVectors;
layout(binding = 1) uniform texture2D	HistoryIn;
layout(binding = 2) uniform texture2D	BaseColor;
layout(binding = 3) uniform texture2D	AETarget;

layout(rgba16f, binding = 4) uniform writeonly image2D		HistoryOut;

layout(binding = 5) uniform sampler		sampLinear;


#define     GAMMA		1.5f
#define     BlendFactor 0.98f


shared vec3 Color[18][18];
shared precise vec2 MotionVector[18][18];


float Luminance(vec3 color)
{
    return dot(color, vec3(0.299, 0.587, 0.114));
}


vec3 ClipColor(vec3 cIn, vec3 cMin, vec3 cMax, vec3 cM, out float l)
{
    vec3 t = 1.f.xxx;

    vec3 diff = sign(cIn - cM) * max(1e-6f.xxx, abs(cIn - cM));

    t = clamp(min(t, max((cMax - cM) / diff, (cMin - cM) / diff)), 0.f, 1.f);
    
    l = min(t.r, min(t.g, t.b));

    return l * (cIn - cM) + cM;
}



vec4 SampleHistory(vec2 uv)
{
    vec2 texSize = textureSize(HistoryIn, 0);
    
    vec2 invSize = 1.f / texSize;
    
    vec2 samplePos = uv * texSize;
    vec2 texPos1 = floor(samplePos - 0.5f) + 0.5f;
    
    vec2 f = samplePos - texPos1;
    
    vec2 w0 = f * (-0.5f + f * (1.f - 0.5f * f));
    vec2 w1 = 1.f + f * f * (-2.5f + 1.5f * f);
    vec2 w2 = f * (0.5f + f * (2.f - 1.5f * f));
    vec2 w3 = f * f * (-0.5f + 0.5f * f);
    
    vec2 w12 = w1 + w2;
    vec2 offset12 = w2 / (w1 + w2);
    
    vec2 texPos0 = texPos1 - 1;
    vec2 texPos3 = texPos1 + 2;
    vec2 texPos12 = texPos1 + offset12;
    
    texPos0 *= invSize;
    texPos3 *= invSize;
    texPos12 *= invSize;

    vec4 result = 0.f.xxxx;
    result += textureLod(sampler2D(HistoryIn, sampLinear), texPos0, 0.f) * w0.x * w0.y;
    result += textureLod(sampler2D(HistoryIn, sampLinear), vec2(texPos12.x, texPos0.y), 0.f) * w12.x * w0.y;
    result += textureLod(sampler2D(HistoryIn, sampLinear), vec2(texPos3.x, texPos0.y), 0.f) * w3.x * w0.y;
    
    result += textureLod(sampler2D(HistoryIn, sampLinear), vec2(texPos0.x, texPos12.y), 0.f) * w0.x * w12.y;
    result += textureLod(sampler2D(HistoryIn, sampLinear), vec2(texPos12.x, texPos12.y), 0.f) * w12.x * w12.y;
    result += textureLod(sampler2D(HistoryIn, sampLinear), vec2(texPos3.x, texPos12.y), 0.f) * w3.x * w12.y;
    
    result += textureLod(sampler2D(HistoryIn, sampLinear), vec2(texPos0.x, texPos3.y), 0.f) * w0.x * w3.y;
    result += textureLod(sampler2D(HistoryIn, sampLinear), vec2(texPos12.x, texPos3.y), 0.f) * w12.x * w3.y;
    result += textureLod(sampler2D(HistoryIn, sampLinear), vec2(texPos3.x, texPos3.y), 0.f) * w3.x * w3.y;
    
    return result;
}



bool isfinite(float x)
{
	return !isinf(x) && !isnan(x);
}



layout (local_size_x = 16, local_size_y = 16, local_size_z = 1) in;
void main()
{
	ivec2 coords		= ivec2(gl_WorkGroupID.xy * gl_NumWorkGroups.xy + gl_LocalInvocationID.xy);
    uint threadIndex	= gl_LocalInvocationID.y * 16 + gl_LocalInvocationID.x;

    ivec2 size = textureSize(BaseColor, 0);

    vec3 c0 = 0.f.xxx;

	vec2 AE = texelFetch(AETarget, ivec2(0), 0).rg * (1.f / 139.26f);

    coords = ivec2(gl_WorkGroupID.xy) * 16 - 1 + ivec2(threadIndex % 18U, threadIndex / 18U);

    c0.rgb = (texelFetch(BaseColor, coords, 0).rgb - AE.g) / (AE.r - AE.g);

    c0 /= 1.f + Luminance(c0.rgb);

    Color[threadIndex / 18U][threadIndex % 18U] = c0;
    MotionVector[threadIndex / 18U][threadIndex % 18U] = texelFetch(MotionVectors, coords, 0).rg;

    if (threadIndex < 68U)
    {
        threadIndex += 256U;
        coords = ivec2(gl_WorkGroupID.xy) * 16 - 1 + ivec2(threadIndex % 18U, threadIndex / 18U);

        c0.rgb = (texelFetch(BaseColor, coords, 0).rgb - AE.g) / (AE.r - AE.g);

        c0 /= 1.f + Luminance(c0.rgb);
        Color[threadIndex / 18U][threadIndex % 18U] = c0;
        MotionVector[threadIndex / 18U][threadIndex % 18U] = texelFetch(MotionVectors, coords, 0).rg;
    }

    memoryBarrierShared();
	barrier();

    coords = ivec2(gl_WorkGroupID.xy * 16 + gl_LocalInvocationID.xy);

    if (coords.x >= size.x || coords.y >= size.y)
        return;

    vec3 mu1 = 0.f.xxx;
    vec3 mu2 = 0.f.xxx;

    vec2 maxMV = 0.f.xx;
    float maxLength = -1.f;
    
    vec3 boxMin = 1e8f.xxx;
    vec3 boxMax = 0.f.xxx;
    ivec2 pos = ivec2(0);

    vec3 currColor = 0.f.xxx;

    for (int i = 0; i < 3; i++)
        for (int j = 0; j < 3; j++)
        {
            pos = ivec2(gl_LocalInvocationID.x + j, gl_LocalInvocationID.y + i);

            vec3 color = Color[pos.y][pos.x];

            mu1 += color.rgb;
            mu2 += color.rgb * color.rgb;

            boxMin = min(boxMin, color.rgb);
            boxMax = max(boxMax, color.rgb);
            
            vec2 mv = MotionVector[pos.y][pos.x];
            float l = dot(mv, mv);
            
            if (l > maxLength)
            {
                maxLength = l;
                maxMV = mv;
            }

            if (i == 1 && j == 1)
                currColor = color.rgb;
        }

    mu1 *= (1.f / 9.f);
    mu2 *= (1.f / 9.f);

    maxMV *= abs(maxMV);

    float v = sqrt(maxLength);

    vec3 sigma = sqrt(max(0.f.xxx, mu2 - mu1 * mu1));

    vec2 reproj_coord = (coords + 0.5f) / vec2(size) - maxMV;

    vec3 res = currColor;

    float l = 0.f;
    
    if (reproj_coord.x * (1.f - reproj_coord.x) > 0.f && reproj_coord.y * (1.f - reproj_coord.y) > 0.f)
    {
        vec4 cAcc = (SampleHistory(reproj_coord) - AE.g) / (AE.r - AE.g);
        cAcc.rgb /= 1.f + Luminance(cAcc.rgb);
        
        float factor = clamp(1.f - 3.f * sqrt(abs(v * 5.f - cAcc.a * cAcc.a) / 5.f), 0.f, 1.f);

        cAcc.rgb = ClipColor(cAcc.rgb, max(0.f.xxx, mu1 - GAMMA * sigma), mu1 + GAMMA * sigma, mu1, l);

        if (!isfinite(cAcc.r) || !isfinite(cAcc.g) || !isfinite(cAcc.b))
            cAcc.rgb = currColor;

        res = mix(currColor, cAcc.rgb, clamp(BlendFactor * factor, 0.f, 1.f));
    }
    
    res /= 1.f - Luminance(res);
	res = res * (AE.r - AE.g) + AE.g;

    imageStore(HistoryOut, coords, vec4(res, sqrt(5.f * v)));
}
